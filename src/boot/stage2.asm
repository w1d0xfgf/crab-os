; ------------------------------------------------------------------
; Stage 2 загрузчика ядра
; ------------------------------------------------------------------

; Компиляция для 16 бит
bits 16

; Смещение 0x8000 к адресам
org 0x8000

; Константы
CODE_SEL equ 0x08		; GDT селектор кода Ring 0
DATA_SEL equ 0x10		; GDT селектор данных Ring 0
SMAP equ 0x534D4150		; 'SMAP'

start:
	; Получить карту памяти

	; Адрес
	mov di, [0x8200 + 2]

	; Для первого вызова функции EBX должен быть 0
	xor ebx, ebx
.next:
	; Получить запись из карты памяти
	push di				; Сохранить адрес
	mov eax, 0xE820		; Функция получения карты памяти
	mov edx, SMAP		; Обязательно
	mov ecx, 24			; 24 байта
	int 0x15			; BIOS вызов
	pop di				; Вернуть адрес
	add di, 24			; Следующая запись в таблице

	; Если ошибка, завершить
	jc .done

	; Если EAX не 'SMAP', завершить
	cmp eax, SMAP
	jne .done

	; Следующая итерация
	test ebx, ebx
	jnz .next
.done:

	; Отключить прерывания
	cli

	; Загрузить GDT
	lgdt [gdt_descriptor] 

	; Включить A20 Line
	call enable_A20

	; Перейти в защищённый режим
	mov eax, cr0			
	or eax, 1
	mov cr0, eax

	; Far-прыжок в ядро для обновления Code Segment
	jmp CODE_SEL:pm_entry

; ------------------------------------------------------------------

; A20 Line
%include "src/boot/a20.asm"

; ------------------------------------------------------------------

; GDT
gdt_start:
	; NULL дескриптор (обязательно)
	dd 0
	dd 0
	
	; Дескриптор сегмента кода
	; Базовый адрес 0, лимит 0xFFFFF, access 0x9A, флаги 0x0C (G=1, D/B=1)
	dw 0xFFFF 		; Лимит (нижние биты)
	dw 0x0000 		; Базовый адрес (нижние биты)
	db 0x00 		; Базовый адрес (средние биты)
	db 10011010b 	; Access
	db 0xCF 		; Флаги + Лимит (верхние биты)
	db 0x00 		; Адрес (верхние биты)

	; Дескриптор сегмента данных
	; Базовый адрес 0, лимит 0xFFFFF, access 0x92, флаги 0x0C
	dw 0xFFFF
	dw 0x0000
	db 0x00
	db 10010010b
	db 0xCF
	db 0x00
gdt_end:

; Дескриптор GDT
gdt_descriptor:
	dw gdt_end - gdt_start - 1 	; Лимит
	dd gdt_start 				; Адрес (32 бит)

; ------------------------------------------------------------------

; Заполнить до 512 байт
times 512 - ($ - $$) db 0 

; Ядро в памяти сразу после Stage 2
pm_entry: